//===== rAthena Script ======================================= 
//= The Airship System Script
//===== By: ================================================== 
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= The Airship System used in the official servers.
//===== Additional Comments: ================================= 
//= 1.0 First version.
//============================================================ 

//============================================================ 
//= Domestic Airship Route
//= ... -> Juno -> Einbroch -> Lighthalzen -> Einbroch
//= -> Juno -> Hugel -> ...
//============================================================ 

airplane,243,73,0	script	#AirshipWarp-1	45,1,1,{
	end;

OnInit:
OnHide:
	specialeffect EF_BASH;
	disablenpc();
	end;

OnUnhide:
	enablenpc();
	specialeffect EF_SUMMONSLAVE;
	end;

OnTouch_:
	switch ($@airplanelocation) {
	case 0: warp "yuno",92,260; end;
	case 1: warp "einbroch",92,278; end;
	case 2: warp "lighthalzen",302,75; end;
	case 3: warp "hugel",181,146; end;
	}
}

airplane,243,29,0	duplicate(#AirshipWarp-1)	#AirshipWarp-2	45,1,1

airplane,1,1,0	script	Domestic_Airship	-1,{
	end;
OnInit:
	initnpctimer;
	end;
OnTimer20000:
	mapannounce "airplane","เรา กำลังมุ่งหน้าไปที่ Einbroch.",bc_map,"0x00ff00";
	end;
OnTimer50000:
	mapannounce "airplane","เรา จะถึง Einbroch เร็ว ๆ นี้ .",bc_map,"0x00ff00";
	end;
OnTimer60000:
	set $@airplanelocation,1;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","ยินดีต้อนรับ สู่ Einbroch. ขอให้เดินทางโดยสวัสดิภาพ.",bc_map,"0x00ff00";
	end;
OnTimer70000:
	mapannounce "airplane","ขณะนี้ เราอยู่ที่ Einbroch เรือเหาะจะออกเดินทางในเร็วๆ นี้ .",bc_map,"0x00ff00";
	end;
OnTimer80000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","เรือเหาะกำลังบินขึ้น จุดหมายต่อไปของเราคือ Lighthalzen.",bc_map,"0x70dbdb";
	end;
OnTimer100000:
	mapannounce "airplane","เรา กำลังมุ่งหน้าไปที่ Lighthalzen.",bc_map,"0x70dbdb";
	end;
OnTimer130000:
	mapannounce "airplane","เรา จะถึง Lighthalzen เร็ว ๆ นี้ .",bc_map,"0x70dbdb";
	end;
OnTimer140000:
	set $@airplanelocation,2;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","ยินดีต้อนรับ สู่ Lighthalzen เดินทางปลอดภัยนะ .",bc_map,"0x70dbdb";
	end;
OnTimer150000:
	mapannounce "airplane","ขณะนี้ เราอยู่ที่ Lighthalzen เรือเหาะจะออกเดินทางในเร็วๆ นี้.",bc_map,"0x70dbdb";
	end;
OnTimer160000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","เรือเหาะกำลังออกจากพื้นดิน จุดหมายต่อไปของเราคือ Einbroch.",bc_map,"0x00ff00";
	end;
OnTimer180000:
	mapannounce "airplane","เรา กำลังมุ่งหน้าไปที่ Einbroch.",bc_map,"0x00ff00";
	end;
OnTimer210000:
	mapannounce "airplane","เรา จะถึง Einbroch เร็ว ๆ นี้ .",bc_map,"0x00FF00";
	end;
OnTimer220000:
	set $@airplanelocation,1;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","ยินดีต้อนรับ สู่ Einbroch. ขอให้เดินทางโดยสวัสดิภาพ .",bc_map,"0x00ff00";
	end;
OnTimer230000:
	mapannounce "airplane","ขณะนี้ เราอยู่ที่ Einbroch เรือเหาะจะออกเดินทางในเร็วๆ นี้ .",bc_map,"0x00ff00";
	end;
OnTimer240000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","เรือเหาะกำลังบินขึ้น จุดหมายต่อไปของเราคือจูโน.",bc_map,"0xff8200";
	end;
OnTimer260000:
	mapannounce "airplane","เรา กำลังมุ่งหน้าไปยังจูโน.",bc_map,"0xff8200";
	end;
OnTimer290000:
	mapannounce "airplane","เรา จะถึงจูโนเร็ว ๆ นี้ .",bc_map,"0xff8200";
	end;
OnTimer300000:
	set $@airplanelocation,0;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","ยินดีต้อนรับ สู่จูโน เดินทางปลอดภัยนะ.",bc_map,"0xff8200";
	end;
OnTimer310000:
	mapannounce "airplane","ขณะนี้ เราอยู่ที่จูโน เรือเหาะจะออกเดินทางในเร็วๆ นี้.",bc_map,"0xff8200";
	end;
OnTimer320000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","เรือเหาะกำลังออกจากพื้นดิน จุดหมายต่อไปของเราคือฮูเกล.",bc_map,"0xca4bf3";
	end;
OnTimer340000:
	mapannounce "airplane","เรา กำลังมุ่งหน้าไปยัง Hugel.",bc_map,"0xca4bf3";
	end;
OnTimer370000:
	mapannounce "airplane","เรา จะถึง Hugel เร็ว ๆ นี้.",bc_map,"0xca4bf3";
	end;
OnTimer380000:
	set $@airplanelocation,3;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","ยินดีต้อนรับ สู่ Hugel เดินทางปลอดภัยนะ.",bc_map,"0xca4bf3";
	end;
OnTimer390000:
	mapannounce "airplane","ขณะนี้ เราอยู่ที่ฮูเกล เรือเหาะจะออกเดินทางในเร็วๆ นี้.",bc_map,"0xca4bf3";
	end;
OnTimer400000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","เรือเหาะกำลังออกจากพื้นดิน จุดหมายต่อไปของเราคือจูโน.",bc_map,"0xff8200";
	end;
OnTimer420000:
	mapannounce "airplane","เรา กำลังมุ่งหน้าไปยังจูโน.",bc_map,"0xff8200";
	end;
OnTimer450000:
	mapannounce "airplane","เรา จะถึงจูโนเร็ว ๆ นี้.",bc_map,"0xff8200";
	end;
OnTimer460000:
	set $@airplanelocation,0;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","ยินดีต้อนรับ สู่จูโน เดินทางปลอดภัยนะ.",bc_map,"0xff8200";
	end;
OnTimer470000:
	mapannounce "airplane","ขณะนี้ เราอยู่ที่จูโน เรือเหาะจะออกเดินทางในเร็วๆ นี้.",bc_map,"0xff8200";
	end;
OnTimer480000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","เรือเหาะกำลังออกจากพื้นดิน จุดหมายต่อไปของเราคือ Einbroch.",bc_map,"0x00ff00";
	stopnpctimer;
	initnpctimer;
}

airplane,240,64,5	script	Exit#airplane1a::ExitAirplane	857,{
	end;
}
airplane,247,64,5	duplicate(ExitAirplane)	Exit#airplane1b	857
airplane,240,40,1	duplicate(ExitAirplane)	Exit#airplane2a	857
airplane,247,40,1	duplicate(ExitAirplane)	Exit#airplane2b	857

airplane,100,69,3	script	Airship Crew#ein-1	852,{
	mes "[Airship Crew]";
	mes "ถ้าเราลงจอดที่";
	mes "จุดหมายปลายทางของคุณและ";
	mes "คุณต้องการออกจาก";
	mes "เรือเหาะกรุณาใช้";
	mes "ขึ้นบันไดข้างหน้าค่ะ ขอบคุณค่ะ";
	mes "สำหรับการอุปถัมภ์ของคุณ.";
	close;
}

airplane,64,94,1	script	Umbala Kid#ein_p	787,{
	emotion ET_PROFUSELY_SWEAT;
	mes "[Kid]";
	if (event_umbala >= 3) {
		mes "ว้าว แม่!";
		mes "ด-ดูนี่สิ!";
		mes "เรากำลังบิน! ร-เรากำลัง...";
		mes "เราอยู่ใน ท้องฟ้า ห่าเหวอะไรเนี่ย !";
	}
	else {
		mes "Makumalagu!";
		mes "Saampa joojimbo";
		mes "kaku na jedi Solo.";
		mes "Bwahahahahahahaah!";
	}
	close;
}

airplane,66,93,3	script	Umbala Lady#ein_p	783,{
	emotion ET_THINK;
	mes "[Lady]";
	if (event_umbala >= 3) {
		mes "เงียบนะ...";
		mes "ที่รัก ทำตัวดีๆ นะ~";
		mes "อย่าทำเป็นตื่นเต้นมากนัก";
		mes "เมื่อเราอยู่ข้างนอก";
		mes "ที่สาธารณะแบบนี้!";
	}
	else {
		mes "Chooktu!";
		mes "Sacraup matii!";
		mes "Shaka gurftalfi";
		mes "huntiki manjoo!";
	}
	close;
}

airplane,71,91,7	script	Umbala Man#ein_p	789,{
	if (event_umbala >= 3) {
		mes "[Chrmlim]";
		mes "เฮ้~";
		mes "จากการมอง";
		mes "หน้าของคุณ, ฉันเข้าใจ";
		mes "คุณเข้าใจ";
		mes "ฉัน. ^333333*โอ้โห...!*^000000";
		next;
		emotion ET_HNG;
		mes "[Chrmlim]";
		mes "ฉันได้ช่วยเหลือ";
		mes "Airship enterprise โดย";
		mes "มีลูกเรือเรือเหาะ";
		mes "ฝึกฝนที่ Umbala เพื่อเอาชนะ";
		mes "พวกเขามีอาการกลัวความสูงจากการกระโดดบันจี้จัมพ์ เจ๋งใช่ไหมล่ะ";
		next;
		mes "[Chrmlim]";
		mes "แต่...";
		mes "บางคนก็ทำไม่ได้";
		mes "เอาชนะความกลัวของพวกเขา";
		mes "ความสูง และบางส่วนเท่ากัน";
		mes "จบลงที่ อืม... ^333333in Niflheim^000000.";
	}
	else {
		mes "[Chrmlim]";
		mes "Bajoo ga";
		mes "nukta Airship.";
		next;
		mes "[Chrmlim]";
		mes "...";
		mes "......";
		next;
		emotion ET_HNG;
		mes "[Chrmlim]";
		mes "Shabala moow bajama";
		mes "Airship kulaha googoona ";
		mes "salu. Dama, kookoo na nu";
		mes "yukuta. Um, fashuku na ret!";
	}
	close;
}

airplane,250,58,2	script	Airship Staff#airplane	67,{
	// Hugel quest addition
	if (hg_ma1 == 3) {
		mes "[Airship Staff]";
		mes "ยินดีต้อนรับ";
		mes "สู่เรือเหาะ.";
		mes "ฉันจะช่วยคุณได้อย่างไร?";
		next;
		select("คุณมีผู้โดยสารชื่อ Thierry ไหม?");
		mes "[Airship Staff]";
		mes "ฉันขอโทษ แต่ฉันไม่คิดว่าเราจะมีผู้โดยสารชื่อนั้น.";
		close;
	}
	// Hugel quest end
	mes "[Airship Staff]";
	mes "ยินดีต้อนรับ";
	mes "สู่เรือเหาะ.";
	mes "ฉันจะช่วยคุณได้อย่างไร";
	next;
	switch (select("Using the Airship:Captain's Cabin:Facilities:ยกเลิก")) {
	case 1:
		mes "[Airship Staff]";
		mes "เมื่อคุณเห็นการออกอากาศ";
		mes "ประกาศว่าเรา";
		mes "ถึงจุดหมายปลายทางแล้ว,";
		mes "กรุณาใช้ทางออกใดทางหนึ่ง";
		mes "ตั้งอยู่ทางทิศเหนือและ";
		mes "ปลายด้านใต้ของเรือเหาะ.";
		next;
		mes "[Airship Staff]";
		mes "หากคุณบังเอิญพลาด";
		mes "ให้คุณหยุด, ไม่ต้องกังวล";
		mes "เรือเหาะอยู่ตลอดเวลา";
		mes "ระหว่าง ทางแล้วคุณจะได้";
		mes "โอกาสอีกครั้งที่จะมาถึง";
		mes "ไปยังจุดหมายปลายทางที่คุณตั้งใจไว้.";
		close;
	case 2:
		mes "[Airship Staff]";
		mes "ห้องโดยสารของกัปตัน";
		mes "ตั้งอยู่ด้านหน้า";
		mes "ของเรือเหาะ นั่นไง คุณ";
		mes "สามารถพบกัปตันและ";
		mes "นักบินของเรือเหาะ";
		close;
	case 3:
		mes "[Airship Staff]";
		mes "เรือเหาะจัดให้";
		mes "มีมินิเกมต่างๆสำหรับ";
		mes "ความบันเทิงของทุกคน";
		mes "ผู้โดยสารของเรา เราขอเชิญ";
		mes "คุณต้องลองเสี่ยงโชคและทักษะของคุณ";
		mes "ในมินิเกมของเรือเหาะ~";
		close;
	case 4:
		mes "[Airship Staff]";
		mes "ในมินิเกมของเรือเหาะ~";
		mes "เที่ยวบินของคุณบนเครื่อง";
		mes "เรือเหาะของพวกเรา ขอบคุณ";
		mes "ขอให้คุณมีวันที่ดีนะ.";
		close;
	}
}

airplane,80,71,2	script	Zerta#01airplane	834,{
	mes "[Zerta]";
	mes "โอ้ สวัสดี นักผจญภัย";
	mes "ฉันกำลังอยู่ใน";
	mes "การเดินทางอันศักดิ์สิทธิ์ การถวาย";
	mes "การอธิษฐานเพื่อประโยชน์ของ";
	mes "ทวีปมิดการ์ด";
	close;
}

airplane,65,63,4	script	Maelin#01airplane	714,{
	mes "[Maelin]";
	mes "เอ่อ นี่คือเรือเหาะ";
	mes "ถึงลูตี้ ไม่ใช่เหรอ? ฉัน";
	mes "รอคอยมานานมาก";
	mes "แต่ฉันไม่ได้ยินอะไรเลย";
	mes "ออกอากาศเกี่ยวกับลูตี้";
	close;
}

airplane,72,34,6	script	Aanos#01airplane	702,{
	mes "[Aanos]";
	mes "โอ้ ว้าว ~";
	mes "ท้องฟ้าดู";
	mes "ต่างกันมากและ";
	mes "สวยจากบนนั้น!";
	close;
}

airplane,221,158,2	script	Pilot#airplane	852,{
	// Hugel quest addition
	if (hg_ma1 == 3) {
		mes "[Pilot]";
		mes "ฉันหวังว่าฉันจะได้ไปดื่มเบียร์เย็นๆ สดๆ สักแก้ว";
		mes "การดื่มคือเป้าหมายในชีวิตของฉัน! การดื่มทำให้ฉันมีพลัง!";
		mes "ฉันไม่มีอะไรเลยถ้าไม่มีเครื่องดื่ม!";
		next;
		mes "[Pilot]";
		mes "แต่! การขับรถขณะเมาสุราไม่ดีนะ";
		mes "แต่! นั่นทำให้ฉันอยากดื่มมากขึ้นเรื่อยๆ นะ!";
		emotion ET_CRY;
		next;
		select("คุณรู้จักผู้โดยสารชื่อ Thierry ไหม?");
		mes "[Pilot]";
		mes "เครื่องแบบนี้คือ";
		mes "เก๋มาก แต่";
		mes "มันหนาเกินไป";
		mes "สวมใส่รอบ ๆ เรือเหาะ";
		next;
		mes "[Pilot]";
		mes "...";
		mes "......";
		mes "ไม่มีใครจริงๆ";
		mes "เข้ามาในห้องนี้";
		mes "และกัปตันก็คือกวางเรนเดียร์ ฉันสามารถถอดเสื้อผ้าเหลือแค่กางเกงบ็อกเซอร์ได้เลย";
		next;
		emotion ET_HUK;
		mes "[Pilot]";
		mes "ห๊ะ!? ใครวะ!";
		next;
		mes "- ...เขาไม่ได้ฟังคุณเลย  -";
		close;
	}
	// Hugel quest end
	switch (rand(1,4)) {
	case 1:
		mes "[Pilot]";
		mes "มันผ่าน ไปแล้ว ";
		mes "นานแล้วที่ฉัน";
		mes "เพลิดเพลินกับอากาศเย็นสบาย";
		mes "เครื่องดื่มที่มีแอลกอฮอล์ แต่";
		mes "งานต้องการให้ฉันเป็นเหมือน";
		mes "มีจิตใจแจ่มใสที่สุดเท่าที่จะทำได้!";
		next;
		mes "[Pilot]";
		mes "ดื่มอย่างมีความรับผิดชอบเสมอ!";
		mes "แต่ฉันยังจำไม่ได้";
		mes "ครั้งสุดท้ายที่ฉันได้ไปเที่ยวพักผ่อนจริงๆ";
		mes "หรือแม้กระทั่งวันหยุด ก็ได้";
		mes "เหล้า มันฝรั่งทอด ทีวี";
		mes "และควรพักผ่อนและฟื้นฟูอย่างจริงจัง";
		emotion ET_CRY;
		close;
	case 2:
		mes "[Pilot]";
		mes "โอ้โห อากาศดีจัง";
		mes "วันนี้ดีจริงๆ";
		mes "ท้องฟ้าสดใสเปิดกว้าง";
		mes "เพื่อความชัดเจนในการมองเห็น";
		mes "และบินอย่างปลอดภัยไร้กังวล";
		close;
	case 3:
		mes "[Pilot]";
		mes "คุณรู้ไหมว่ากัปตันของเราเป็น";
		mes "ผู้ชายที่น่าเคารพ เขาและ";
		mes "พี่ชายของเขาสบายดี";
		mes "เป็นที่รู้จักในอุตสาหกรรมการบิน";
		mes "ใครจะรู้ว่ากวางเรนเดียร์สร้างขึ้น";
		mes "กัปตันเก่งมากเหรอ?";
		next;
		mes "[Pilot]";
		mes "ระหว่างคุณสองคน";
		mes "และฉัน ฉัน ต้อง บอก";
		mes "คุณ ซานต้าคนนั้นน่ะเหรอ";
		mes "อะไรบางอย่าง, ได้กวางเรนเดียร์";
		mes "และพวกเอลฟ์มาทำงานให้เขา";
		mes "ผู้ชายคนนี้ต้องเป็นอัจฉริยะแน่ๆ!";
		close;
	default:
		mes "[Pilot]";
		mes "คุณรู้ไหมว่าทั้งหมดนี้";
		mes "สิ่งที่นำร่องอยู่กลางอากาศ";
		mes "มันค่อนข้างใหม่นะรู้ไหม";
		mes "ใช่แล้ว พวกเขาได้เรือเหาะลำนี้มา";
		mes "การดำเนินการอย่างเร่งรีบ";
		next;
		emotion ET_HUK;
		mes "[Pilot]";
		mes "แต่พวกมันก็เป็นของจริง";
		mes "จริงจังนะ คิดจริงๆ";
		mes "ข้างหน้า ฉันหมายถึง พวกเขาจับตัวเราไว้แล้ว";
		mes "การฝึกฝนขณะอยู่บนเรือเหาะ";
		mes "ยังอยู่ในช่วงที่ถูกประดิษฐ์ขึ้น";
		mes "นั่นมัน บ้าไป แล้ว เหรอ!";
		close;
	}
}

airplane,50,66,5	script	Apple Merchant#airplane	86,{
	mes "[Fruitz]";
	mes "ยินดีต้อนรับสู่ Fruitz";
	mes "ช้อปได้ทุกที่ที่คุณสามารถ";
	mes "ซื้อแอปเปิ้ลหรือบด";
	mes "พวกเขาทำน้ำแอปเปิ้ล";
	next;
	switch (select("ซื้อ แอปเปิ้ล: ทำน้ำ แอปเปิ้ล: คุณมาที่นี่ทำไม: ยกเลิก")) {
	case 1:
		mes "[Fruitz]";
		mes "กรุณากรอกจำนวน";
		mes "ของแอปเปิ้ลที่คุณต้องการ";
		mes "ซื้อ แอปเปิลลูกละ 15 เซนี ";
		mes "และคุณสามารถซื้อได้สูงสุด";
		mes "ครั้งละ 500 กรุณากรอก";
		mes " '0' เพื่อยกเลิกคำสั่งซื้อของคุณ";
		next;
		while (1) {
			input .@input;
			set .@pay, .@input * 15;
			if (.@input == 0) {
				mes "[Fruitz]";
				mes "ขอบคุณที่แวะมา";
				mes "ที่ร้านของฉัน ลาก่อน!";
				mes "แวะมาเมื่อไหร่ก็ได้";
				mes "คุณรู้สึกเหมือนมี";
				mes "แอปเปิ้ลไว้ทานเล่น~";
				close;
			}
			else if (.@input < 1 || .@input > 500) {
				mes "[Fruitz]";
				mes "คุณป้อนหมายเลข";
				mes "สูงกว่าสูงสุด";
				mes "มูลค่า 500 กรุณากรอก";
				mes "จำนวนแอปเปิลของคุณ";
				mes "ต้องการซื้ออีกครั้ง";
				next;
			}
			else {
				mes "[Fruitz]";
				mes "A total of ^FF0000" + .@input + "^000000 Apples";
				mes "will cost you ^FF0000" + .@pay + "^000000 zeny.";
				mes "Would you like to continue?";
				next;
				if (select("Yes:No") == 2) {
					mes "[Fruitz]";
					mes "ขอบคุณที่แวะมา";
					mes "ที่ร้านของฉัน ลาก่อน!";
					mes "แวะมาเมื่อไหร่ก็ได้";
					mes "คุณรู้สึกเหมือนมี";
					mes "แอปเปิ้ลไว้ทานเล่น~";
					close;
				}
				break;
			}
		}
		if (Zeny < .@pay) {
			mes "[Fruitz]";
			mes "ฉันขอโทษ แต่คุณไม่ได้";
			mes "มีเงินพอที่จะ";
			mes "ซื้อแอปเปิลจำนวนนั้น";
			mes "โปรดตรวจสอบเงิน zeny ของคุณ หรือ";
			mes "ซื้อแอปเปิลน้อยลง";
			close;
		}
		else if (checkweight(512,.@input) == 0) {
			mes "[Fruitz]";
			mes "อืมม ฉันไม่คิดนะ";
			mes "คุณมีพื้นที่เพียงพอ";
			mes "สินค้าคงคลังของคุณเพื่อดำเนินการสิ่งนี้";
			mes "แอปเปิ้ลเยอะแยะ ทำไมคุณไม่เคลียร์พื้นที่เก็บของของคุณบ้างล่ะ";
			close;
		}
		else {
			set Zeny, Zeny - .@pay;
			getitem 512,.@input; //Apple
			mes "[Fruitz]";
			mes "ขอบคุณที่มาเยี่ยมชม";
			mes "ร้านของฉัน ฉันหวังว่าคุณจะชอบ";
			mes "รสชาติของแอปเปิ้ลเหล่านี้~!";
			close;
		}
	case 2:
		mes "[Fruitz]";
		mes "โอเค ฉันต้องการ";
		mes "^FF00003 แอปเปิลและขวดเปล่า 1 ขวด^000000";
		mes "ทำน้ำแอปเปิ้ล 1 แก้วให้คุณ";
		mes "คุณต้องการดำเนินการต่อหรือไม่";
		next;
		switch (select("ใช่:ไม่ใช่ ")) {
		case 1:
			if (countitem(512) < 3 || countitem(713) < 1) {
				mes "[Fruitz]";
				mes "ฉันขอโทษ แต่คุณไม่ได้";
				mes "มีวัสดุเพียงพอ";
				mes "สร้างขวดน้ำแอปเปิ้ล";
				mes "จำไว้ว่าฉันต้องการแอปเปิล 3 ลูก";
				mes "และขวดเปล่า 1 ขวดเพื่อทำเช่นนั้น";
				close;
			}
			else {
				mes "[Fruitz]";
				mes "ขอบคุณ,";
				mes "กรุณารอสักครู่";
				mes "รอสักครู่";
				next;
				mes "^3355FF*บด บด*";
				mes "*บด บด*";
				mes "*กริ๊ง ...!*^000000";
				next;
				delitem 512,3; //Apple
				delitem 713,1; //Empty_Bottle
				getitem 531,1; //Apple_Juice
				mes "[Fruitz]";
				mes "ไปกันเลย~";
				mes "ฉันหวังว่าคุณจะสนุก!";
				mes "กรุณาอย่าลังเลที่จะ";
				mes "แวะมาซื้อ Apple ของคุณสิ";
				mes "และความต้องการของน้ำแอปเปิล";
				mes "เมื่อ ไหร่ก็ได้ นักผจญภัย~";
				close;
			}
		case 2:
			mes "[Fruitz]";
			mes "ขอบคุณที่แวะมา";
			mes "ที่ร้านของฉัน ลาก่อน!";
			mes "แวะมาเมื่อไหร่ก็ได้";
			mes "คุณรู้สึกเหมือนมี";
			mes "แอปเปิ้ลไว้ทานเล่น~";
			close;
		}
	case 3:
		mes "[Fruitz]";
		mes "ฉันเคยเป็นคนพเนจร";
		mes "คนพเนจรเมื่อวันหนึ่ง";
		mes "ฉันงีบสักพักแล้วก็ทำอะไรบางอย่าง";
		mes "กระแทกหัวฉันแล้วตื่นขึ้น";
		mes "ฉันจากการหลับอันผ่อนคลาย";
		next;
		mes "[Fruitz]";
		mes "กลายเป็นว่าฉันเป็น";
		mes "นอนอยู่ใต้แอปเปิล";
		mes "ต้นไม้และแอปเปิ้ลหล่นลงมา";
		mes "และตีหัวฉัน";
		mes "ฉันกำลังจะตายเพราะความหิวโหยและ";
		mes "กำลังจะกินแอปเปิลนั่น..";
		next;
		mes "[Fruitz]";
		mes "แต่ทันใดนั้น ไคน์ ฉันก็แก่แล้ว";
		mes "เพื่อนสมัยทำเหมืองแร่";
		mes "ขอให้ฉันช่วยเขา";
		mes "บนเรือเหาะ ฉันก็ทำ อย่างนั้น และ";
		mes "ฉันพบมันที่นั่น";
		mes "ผู้คนกำลังเล่นเกมลูกเต๋า";
		next;
		mes "[Fruitz]";
		mes "ฉันเบื่อและอยากรู้อยากเห็น";
		mes "และจบลงด้วยการเดิมพันว่า";
		mes "แอปเปิลหนึ่งลูกในเกม";
		mes "ลูกเต๋า แต่ด้วยเหตุผลบางอย่าง";
		mes "ฉันมีโชคดีอย่างเหลือเชื่อ";
		mes "สตรีค. แอปเปิลหนึ่งลูกกลายเป็นสองลูก... ";
		next;
		mes "[Fruitz]";
		mes "สองกลายเป็นสี่และ";
		mes "ก่อนที่ฉันจะรู้ตัว ฉันก็มี";
		mes "ผูกขาดตลาด Apple!";
		mes "ฉันชนะแอปเปิ้ลมาหลายลูกเลย";
		mes "ฉัน เริ่มธุรกิจของตัวเองที่นี่";
		mes "บนเรือเหาะ แปลกใช่ไหมล่ะ?";
		next;
		mes "[Fruitz]";
		mes "แอปเปิ้ลก็ดี";
		mes "สำหรับคุณ พวกเขาเป็น";
		mes "ดีมากจริงๆ";
		mes "ถึงฉัน. ฮ่าฮ่าฮ่าฮ่า ~!";
		close;
	case 4:
		mes "[Fruitz]";
		mes "ขอบคุณสำหรับ";
		mes "ใช้ร้านค้าของฉัน";
		mes "ลาก่อน~";
		close;
	}
}

//============================================================ 
//= International Airship Route
//= ... -> Izlude -> Juno -> Rachel -> ...
//============================================================ 

airplane_01,243,73,0	script	#AirshipWarp-3	45,1,1,{
	end;
	
OnTouch_:
	switch ($@airplanelocation2) {
	case 0: warp "ra_fild12",292,204; end;
	case 1: 
		if(checkre(0))
			warp "izlude",200,73;
		else
			warp "izlude",200,56;
		end;
	case 2: warp "yuno",12,261; end;
	}

OnInit:
OnHide:
	specialeffect EF_BASH;
	disablenpc();
	end;
	
OnUnhide:
	enablenpc();
	specialeffect EF_SUMMONSLAVE;
	end;
}

airplane_01,243,29,0	duplicate(#AirshipWarp-3)	#AirshipWarp-4	45,1,1

airplane_01,1,1,0	script	International_Airship	-1,{
	end;
OnInit:
OnEnable:
	initnpctimer;
	end;
OnTimer25000:
	mapannounce "airplane_01","เรากำลังมุ่งหน้าไปที่ Izlude",bc_map,"0x00ff00";
	end;
OnTimer50000:
	mapannounce "airplane_01","เราจะถึง Izlude เร็ว ๆ นี้ .",bc_map,"0x00ff00";
	end;
OnTimer60000:
	set $@airplanelocation2,1;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","ยินดีต้อนรับสู่ Izlude เดินทางปลอดภัยนะ",bc_map,"0x00ff00";
	end;
OnTimer70000:
	mapannounce "airplane_01","ขณะนี้เราอยู่ที่ Izlude เรือเหาะจะออกเดินทางในเร็วๆ นี้ .",bc_map,"0x00ff00";
	end;
OnTimer80000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","เรือเหาะกำลังบินขึ้นแล้ว จุดหมายต่อไปของเราคือจูโน ",bc_map,"0x70dbdb";
	end;
OnTimer105000:
	mapannounce "airplane_01","เรากำลังมุ่งหน้าไปยังจูโน ",bc_map,"0x70dbdb";
	end;
OnTimer130000:
	mapannounce "airplane_01","เราจะถึงจูโนเร็วๆ นี้ ",bc_map,"0x70dbdb";
	end;
OnTimer140000:
	set $@airplanelocation2,2;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","ยินดีต้อนรับสู่จูโน เดินทางปลอดภัยนะ .",bc_map,"0x70dbdb";
	end;
OnTimer150000:
	mapannounce "airplane_01","ขณะนี้เราอยู่ที่จูโน เรือเหาะจะออกเดินทางในเร็วๆ นี้ ",bc_map,"0x70dbdb";
	end;
OnTimer160000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","เรือเหาะกำลังออกจากพื้นดิน จุดหมายต่อไปของเราคือ Rachel ",bc_map,"0xFF8200";
	end;
OnTimer185000:
	mapannounce "airplane_01","เรากำลังมุ่งหน้าไปหา Rachel ",bc_map,"0xFF8200";
	end;
OnTimer210000:
	mapannounce "airplane_01","เราจะถึง Rachel เร็ว ๆ นี้ ",bc_map,"0xFF8200";
	end;
OnTimer220000:
	set $@airplanelocation2,0;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","ยินดีต้อนรับ Rachel เดินทางปลอดภัยนะ ",bc_map,"0xFF8200";
	end;
OnTimer230000:
	mapannounce "airplane_01","ขณะนี้เราอยู่ที่ Rachel เรือเหาะจะออกเดินทางในเร็วๆ นี้ ",bc_map,"0xFF8200";
	end;
OnTimer240000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","เรือเหาะกำลังบินขึ้นแล้ว จุดหมายต่อไปของเราคือ Izlude ",bc_map,"0x00ff00";
	stopnpctimer;
	set .moninv, .moninv + 1;
	if (.moninv == 7) {
		if (rand(1,3) == 3) {
			donpcevent "Airship#airplane02::OnEnable";
			end;
		}
		set .moninv, 0;
	}
	initnpctimer;
	end;
}

airplane_01,240,64,5	script	Exit#airplane_011a::ExitAirplane01	857,{
	end;
}
airplane_01,247,64,5	duplicate(ExitAirplane01)	Exit#airplane_011b	857
airplane_01,240,40,1	duplicate(ExitAirplane01)	Exit#airplane_012a	857
airplane_01,247,40,1	duplicate(ExitAirplane01)	Exit#airplane_012b	857

airplane_01,250,58,2	script	Airship Staff#airplane01	67,{
	mes "[Airship Staff]";
	mes "ยินดีต้อนรับ";
	mes "ถึงเรือเหาะ";
	mes "ฉันจะช่วยคุณได้อย่างไร?";
	next;
	switch (select("Using the Airship:Captain's Cabin:Facilities:ยกเลิก")) {
	case 1:
		mes "[Airship Staff]";
		mes "เมื่อคุณเห็นการออกอากาศ";
		mes "ประกาศว่าเรา";
		mes "ถึงจุดหมายปลายทางแล้ว";
		mes "กรุณาใช้ทางออกใดทางหนึ่ง";
		mes "ตั้งอยู่ทางทิศเหนือและ";
		mes "ปลายด้านใต้ของเรือเหาะ.";
		next;
		mes "[Airship Staff]";
		mes "หากคุณบังเอิญพลาด";
		mes "หยุดแล้วนะ ไม่ต้องกังวล";
		mes "เรือเหาะอยู่ตลอดเวลา";
		mes "ระหว่าง ทางแล้วคุณจะได้";
		mes "โอกาสอีกครั้งที่จะมาถึง";
		mes "ไปยังจุดหมายปลายทางที่คุณตั้งใจไว้";
		close;
	case 2:
		mes "[Airship Staff]";
		mes "ห้องโดยสารของกัปตัน";
		mes "ตั้งอยู่ด้านหน้า";
		mes "ของเรือเหาะ นั่นไง คุณ";
		mes "สามารถพบกัปตันและ";
		mes "นักบินของเรือเหาะ";
		close;
	case 3:
		mes "[Airship Staff]";
		mes "เรือเหาะจัดให้";
		mes "มินิเกมต่างๆสำหรับ";
		mes "ความบันเทิงของทุกคน";
		mes "ผู้โดยสารของเรา เราขอเชิญ";
		mes "คุณต้องลองเสี่ยงโชคและทักษะของคุณ";
		mes "ในมินิเกมของเรือเหาะ~";
		close;
	case 4:
		mes "[Airship Staff]";
		mes "ฉันหวังว่าคุณ";
		mes "เที่ยวบินของคุณบนเครื่อง";
		mes "เรือเหาะของพวกเรา ขอบคุณ";
		mes "ขอให้คุณมีวันที่ดีนะ";
		close;
	}
}

airplane_01,50,66,5	script	Apple Merchant#air01	86,{
	if (checkweight(1201,1) == 0) {
		mes "- รอก่อน นะ !!  -";
		mes "- ขณะนี้คุณกำลังพกพา  -";
		mes "- มีของกับคุณมากเกินไป  -";
		mes "- กรุณาลองอีกครั้ง  -";
		mes "- หลังจากที่คุณลดน้ำหนักบ้างแล้ว  -";
		close;
	}
	mes "[Meltz]";
	mes "ยินดีต้อนรับสู่ Meltz";
	mes "ช้อปได้ทุกที่ที่คุณสามารถ";
	mes "ซื้อแอปเปิ้ลหรือบด";
	mes "พวกเขาทำน้ำแอปเปิ้ล";
	next;
	switch (select("Buy Apples.:Make Apple Juice.:Cancel.")) {
	case 1:
		mes "[Meltz]";
		mes "กรุณากรอกจำนวน";
		mes "ของแอปเปิ้ลที่คุณต้องการ";
		mes "ซื้อ แอปเปิลลูกละ 15 เซนี ";
		mes "และคุณสามารถซื้อได้สูงสุด";
		mes "ครั้งละ 500 กรุณากรอก";
		mes "'0' เพื่อยกเลิกคำสั่งซื้อของคุณ";
		next;
		while (1) {
			input .@input;
			set .@pay, .@input * 15;
			if (.@input == 0) {
				mes "[Meltz]";
				mes "ขอบคุณที่แวะมา";
				mes "ที่ร้านของฉัน ลาก่อน!";
				mes "แวะมาเมื่อไหร่ก็ได้";
				mes "คุณรู้สึกเหมือนมี";
				mes "แอปเปิ้ลไว้ทานเล่น~";
				close;
			}
			else if (.@input < 1 || .@input > 500) {
				mes "[Meltz]";
				mes "คุณป้อนหมายเลข";
				mes "สูงกว่าสูงสุด";
				mes "มูลค่า 500 กรุณากรอก";
				mes "จำนวนแอปเปิลของคุณ";
				mes "ต้องการซื้ออีกครั้ง";
				next;
			}
			else {
				mes "[Meltz]";
				mes "A total of ^FF0000" + .@input + "^000000 Apples";
				mes "will cost you ^FF0000" + .@pay + "^000000 zeny.";
				mes "Would you like to continue?";
				next;
				if (select("ใช่:ไม่ใช่ ") == 2) {
					mes "[Meltz]";
					mes "ขอบคุณที่แวะมา";
					mes "ที่ร้านของฉัน ลาก่อน!";
					mes "แวะมาเมื่อไหร่ก็ได้";
					mes "คุณรู้สึกเหมือนมี";
					mes "แอปเปิ้ลไว้ทานเล่น~";
					close;
				}
				break;
			}
		}
		if (Zeny < .@pay) {
			mes "[Meltz]";
			mes "ฉันขอโทษ คุณไม่มี";
			mes "พอแล้วกับเงินของคุณ";
			mes "โปรดตรวจสอบเงินของคุณหรือ";
			mes "ซื้อแอปเปิลให้น้อยลง";
			close;
		}
		else if (checkweight(512,.@input) == 0) {
			mes "[Meltz]";
			mes "อืม ฉันไม่คิดว่าคุณจะมี";
			mes "มีเนื้อที่พอที่จะถือ";
			mes "แอปเปิ้ลมากขนาดนี้ คุณอาจจะได้";
			mes "ต้องการเพิ่มสินค้าคงคลังของคุณ";
			mes "พื้นที่";
			close;
		}
		else {
			set Zeny, Zeny - .@pay;
			getitem 512,.@input; //Apple
			mes "[Meltz]";
			mes "ขอบคุณที่มาเยี่ยมชม";
			mes "ร้านของฉัน ฉันหวังว่าคุณจะชอบ";
			mes "รสชาติของแอปเปิ้ลเหล่านี้~!";
			close;
		}
	case 2:
		mes "[Meltz]";
		mes "โอเค ฉันต้องการ";
		mes "^FF00003 แอปเปิลและขวดเปล่า 1 ขวด^000000";
		mes "ทำน้ำแอปเปิ้ล 1 แก้วให้คุณ";
		mes "คุณต้องการดำเนินการต่อหรือไม่";
		next;
		switch (select("ใช่:ไม่ใช่ ")) {
		case 1:
			if (countitem(512) < 3 || countitem(713) < 1) {
				mes "[Meltz]";
				mes "ฉันขอโทษ แต่คุณไม่ได้";
				mes "มีวัสดุเพียงพอ";
				mes "สร้างขวดน้ำแอปเปิ้ล";
				mes "จำไว้ว่าฉันต้องการแอปเปิล 3 ลูก";
				mes "และขวดเปล่า 1 ขวดเพื่อทำเช่นนั้น";
				close;
			}
			else {
				mes "[Meltz]";
				mes "ขอบคุณ กรุณารอสักครู่";
				next;
				mes "^3355FF*บด* *บด*";
				mes "*บด* *บด*";
				mes "*กริ๊ง ...!*^000000";
				next;
				delitem 512,3; //Apple
				delitem 713,1; //Empty_Bottle
				getitem 531,1; //Apple_Juice
				mes "[Meltz]";
				mes "ไปกันเลย~";
				mes "โปรดมาอีกครั้ง";
				close;
			}
		case 2:
			mes "[Meltz]";
			mes "ขอบคุณที่แวะมา";
			mes "ที่ร้านของฉัน ลาก่อน!";
			mes "แวะมาเมื่อไหร่ก็ได้";
			mes "คุณรู้สึกเหมือนมี";
			mes "แอปเปิ้ลไว้ทานเล่น~";
			close;
		}
	case 3:
		mes "[Meltz]";
		mes "ขอบคุณที่แวะมา";
		mes "ที่ร้านของฉัน ลาก่อน!";
		mes "แวะมาเมื่อไหร่ก็ได้";
		mes "คุณรู้สึกเหมือนมี";
		mes "แอปเปิ้ลไว้ทานเล่น~";
		close;
	}
}

airplane_01,221,158,2	script	Pilot#airplane_01	852,{
	switch (rand(1,4)) {
	case 1:
		mes "[Pilot]";
		mes "ลองจิจูด 131 องศาตะวันออก";
		mes "ละติจูด 37 องศาเหนือ";
		mes "เรากำลังดำเนินไปอย่างถูกต้อง กัปตัน";
		close;
	case 2:
		mes "[Pilot]";
		mes "ดูเหมือนจริงๆ";
		mes "วันฟ้าครึ้ม ยากเสมอ";
		mes "เพื่อนำทางเมื่อท้องฟ้าแจ่มใส";
		mes "ไม่ชัดเจน เดาว่าเราจะ";
		mes "จำเป็นต้องขยายเรดาร์";
		close;
	case 3:
		mes "[Pilot]";
		mes "กัปตันเป็นคนดี";
		mes "ฉันกับผู้ชายคิดไม่ออก";
		mes "บุคคลที่มีความเหมาะสมในการบังคับบัญชา";
		mes "เรือลำนี้ ยังไงก็ตาม เขาก็ยังสวย";
		mes "แกร่ง ทาสผู้แท้จริง";
		next;
		mes "[^ff0000Tarlock^000000]";
		mes "^ff0000Hey...!^000000";
		mes "^ff0000Less chit-chat^000000";
		mes "^ff0000and more piloting!^000000";
		next;
		mes "[Pilot]";
		mes "ท-ทันทีเลยครับท่าน!";
		mes "(เห็นไหมว่าฉันหมายถึงอะไร)";
		close;
	default:
		mes "[Pilot]";
		mes "เครื่องแบบนี้คือ";
		mes "เก๋มาก แต่";
		mes "มันหนาเกินไป";
		mes "สวมใส่รอบ ๆ เรือเหาะ";
		next;
		mes "[Pilot]";
		mes "...";
		mes "......";
		mes "ไม่มีใครจริงๆเลย";
		mes "เข้ามาในห้องนี้";
		mes "และกัปตันก็คือกวางเรนเดียร์";
		mes "ฉันแค่ถอดเสื้อผ้าเหลือแค่กางเกงบ็อกเซอร์ก็พอ";
		next;
		emotion ET_HUK;
		mes "[Pilot]";
		mes "โอ้...! สวัสดี!";
		mes "เอิ่ม-สนุกกับเที่ยวบินของคุณเหรอ?!";
		close;
	}
}

airplane_01,83,61,2	script	Dianne#01airplane_01	72,2,2,{
	mes "[Dianne]";
	mes "มันแปลกมาก!";
	mes "ฉันไปเยี่ยมชม";
	mes "กัปตันเรือเหาะและ";
	mes "สิ่งเดียวที่ฉันเห็นก็คือสิ่งนี้";
	mes "กวางเรนเดียร์ประหลาดๆ โอ้!";
	mes "คุณคิดว่า...";
	close;

OnTouch_:
	emotion ET_CRY;
	end;
}

airplane_01,69,63,2	script	Mendel#01airplane_01	55,{
	mes "[Mendel]";
	mes "อย่างที่ฉันคาดหวังไว้";
	mes "อาหารบนเที่ยวบินคือ";
	mes "คุณภาพ สามดาว ที่ดีที่สุด.";
	mes "*Harrrumph* ฉันควรจะทำจริงๆ";
	mes "ได้พาเชฟของฉันมาเพื่อว่า";
	mes "ฉันสามารถเพลิดเพลินไปกับอาหารมื้อจริงได้";
	close;
}

airplane_01,71,31,2	script	Swordsman Shimizu#air_01	106,{
	mes "[Swordsman Shimizu]";
	mes "ในที่สุด หลังจากห้า";
	mes "หลายปีแห่งการรอคอย...";
	mes "ฉันสามารถแก้แค้นได้!";
	next;
	mes "[Swordsman Shimizu]";
	mes "ฉันแค่...";
	mes "ต้องแน่ใจว่า";
	mes "ฉันไม่ได้คิดถึงเรื่องของฉันอยู่เรื่อย";
	mes "หยุด แต่อีกไม่นาน เร็วๆ นี้";
	mes "การแก้แค้นจะเป็นของฉัน!";
	close;
}

//============================================================ 
//= Typing Challenge
//============================================================ 
airplane_01,32,61,4	script	Nils#ein	49,1,1,{
	mes "[Nils]";
	mes "ยินดีต้อนรับสู่";
	mes "^ff0000RO Typing Challenge^000000.";
	mes "คุณอยากเล่นมั้ย";
	mes "เกมพิมพ์เร็ว ๆ นี้เหรอ?";
	next;
	switch (select("Play ^ff0000RO Typing Challenge^000000:Information:View Top Records:ยกเลิก")) {
	case 1:
		mes "[Nils]";
		mes "โอเค เรามี";
		mes "ผู้ท้าชิงรายใหม่!";
		mes "ระบุข้อมูลต่อไปนี้";
		mes "ส่งข้อความเร็วเท่าคุณ";
		mes "สามารถทำได้โดยไม่ต้องทำอะไรเลย";
		mes "ผิดพลาด! เริ่มกันเลย~!";
		setarray .@line1_1$[0],	"^3cbcbccallipygian salacius lascivious^000000",
					"^3cbcbcBy the power of^000000",
					"^0000ffthkelfkskeldmsiejdlslehfndkelsheidl^000000",
					"^3cbcbcburrdingdingdingdilidingdingdingphoohudaamb^000000",
					"^3cbcbcCoboman no chikara-yumei na^000000",
					"^3cbcbcI'm the king of All Weirdos! Now^000000",
					"^3cbcbcYou give me no choice. I guess it's^000000";
		setarray .@line1_2$[0],	"^3cbcbclicentious prurient concupiscent^000000",
					"^3cbcbcp-po-poi-po-poi-poin-poing^000000",
					"^3cbcbcskemd^000000",
					"^3cbcbcandoorabambarambambambambamburanbamding^000000",
					"^3cbcbcchikara-daiookii na chikara da ze!^000000",
					"^3cbcbcyou know of my true power. Obey~!^000000",
					"^3cbcbctime for me to reveal my secret...^000000";
		setarray .@line1_3$[0],	"",
					"^3cbcbcGOD-POING. I NEVER LOSE!^000000",
					"",
					"",
					"^3cbcbcCOBO ON^000000",
					"",
					"";
		setarray .@word1$[0],	"callipygian salacius lascivious licentious prurient concupiscent",
					"By the power of p-po-poi-po-poi-poin-poing GOD-POING. I NEVER LOSE!",
					"thkelfkskeldmsiejdlslehfndkelsheidlskemd",
					"burrdingdingdingdilidingdingdingphoohudaambandoorabambarambambambambamburanbamding",
					"Coboman no chikara-yumei na chikara-daiookii na chikara da ze! COBO ON",
					"I'm the king of All Weirdos! Now you know of my true power. Obey~!",
					"You give me no choice. I guess it's time for me to reveal my secret...";
		setarray .@line2_1$[0],	"^3cbcbcuNflAPPaBLe LoVaBLe SeCreTs AnD^000000",
					"^ff1493LiGhTsPeEd RiGhT SPEed LeFT TURn^000000",
					"^ff1493hfjdkeldjsieldjshfjdjeiskdlefvbd^000000",
					"^ff1493burapaphuralanderamduanbatuhiwooi^000000",
					"^ff1493belief love luck grimace sweat rush^000000",
					"^800080opeN, Open!op3n.openOpen0p3nOpEn0pen^000000",
					"^3cbcbcfReeDoM ecstAcy JoUrnaliSm ArMplt^000000";
		setarray .@line2_2$[0],	"^3cbcbcboWLIiNg aGaINST tHe KarMA of YoUtH^000000",
					"^ff1493RiGhT BuRn OrIGInAL GaNgSteR SmACk^000000",
					"",
					"^ff1493kabamturubamdingding^000000",
					"^ff1493folktale rodimus optimus bumblebee^000000",
					"^800080`open'0Pen open? open!111OPENSESAME^000000",
					"^3cbcbcDisCoverY hEaDaChE MoonbeAmS jUsTiCE^000000";
		setarray .@word2$[0],	"uNflAPPaBLe LoVaBLe SeCreTs AnD boWLIiNg aGaINST tHe KarMA of YoUtH",
					"LiGhTsPeEd RiGhT SPEed LeFT TURn RiGhT BuRn OrIGInAL GaNgSteR SmACk",
					"hfjdkeldjsieldjshfjdjeiskdlefvbd",
					"burapaphuralanderamduanbatuhiwooikabamturubamdingding",
					"belief love luck grimace sweat rush folktale rodimus optimus bumblebee",
					"opeN, Open!op3n.openOpen0p3nOpEn0pen`open'0Pen open? open!111OPENSESAME",
					"fReeDoM ecstAcy JoUrnaliSm ArMplt DisCoverY hEaDaChE MoonbeAmS jUsTiCE";
		setarray .@letters[0],	1300,
					1250,
					1180,
					1380,
					1740,
					1440,
					1450;
		set .@wordtest, rand(7);
		next;
		mes "[Nils]";
		mes .@line1_1$[.@wordtest];
		mes .@line1_2$[.@wordtest];
		mes .@line1_3$[.@wordtest];
		set .@start_time, gettimetick(1);
		next;
		input .@save1$;
		set .@end_time, gettimetick(1);
		set .@total_time, .@end_time - .@start_time;
		mes "[Nils]";
		mes .@line2_1$[.@wordtest];
		mes .@line2_2$[.@wordtest];
		set .@start_time, gettimetick(1);
		next;
		input .@save2$;
		set .@end_time, gettimetick(1);
		set .@total_time, .@total_time + (.@start_time - .@end_time);
		set .@tasoo, (.@letters[.@wordtest] / ( .@total_time > 0 ?  .@total_time : 1)) * 6;
		if ((.@save1$ == .@word1$[.@wordtest]) && (.@save2$ == .@word2$[.@wordtest])) {
			mes "[Nils]";
			mes "Your record is ^ff0000" + .@total_time + " seconds^000000 and";
			mes "the total letters are " + .@tasoo + ".";
			next;
			if (.@tasoo >= 1300) {
				mes "[Nils]";
				mes "อืมม แผ่นเสียงนี้ไม่ใช่";
				mes "เป็นไปได้ในทางมนุษย์ เว้นแต่คุณ";
				mes "คัดลอกและวางทั้งหมด";
				mes "ประโยคนี้ โปรดเล่นอย่างยุติธรรม";
				mes "ครั้งหน้า";
				close;
			}
			if (.@tasoo >= $050320_ein_typing) {
				mes "[Nils]";
				mes "สถิติสูงสุดก่อนหน้านี้คือ";
				mes "สร้างโดย ^0000ff" + $050320_minus1_typing$ + "^000000";
				mes "มียอดรวม  ^0000ff" + $050320_ein_typing + "^000000 ตัวอักษร";
				mes "อย่างไรก็ตาม, ^ff0000" + strcharinfo(0) + "^000000,";
				mes "คุณได้สร้างสถิติสูงสุดใหม่";
				mes "ครั้งนี้ ขอแสดงความยินดีด้วย!";
				set $050320_minus1_typing$, strcharinfo(0);
				set $050320_ein_typing, .@tasoo;
				close;
			}
			else {
				mes "[Nils]";
				mes "^0000ff" + $050320_minus1_typing$ + "^000000";
				mes "is the current";
				mes "record holder with";
				mes "a letter total of ^0000ff" + $050320_ein_typing + "^000000";
				mes "characters. Try to beat";
				mes "that record next time~";
				close;
			}
		}
		else {
			mes "[Nils]";
			mes "โอ้ ...";
			mes "ฉันขอโทษแต่";
			mes "คุณเข้ามา";
			mes "ข้อความไม่ถูกต้อง...";
			close;
		}
	case 2:
		mes "[Nils]";
		mes "The ^ff0000RO Typing Challenge^000000";
		mes "เป็นเกมที่คุณเข้าไป";
		mes "ข้อความที่กำหนดให้เร็วที่สุดเท่าที่คุณ";
		mes "สามารถครับ ชื่อผู้เล่นตัวท็อป";
		mes "ถูกบันทึกไว้เพื่อคนรุ่นหลัง ถ้าคุณ";
		mes "อยากมีชื่อเสียง นี่คือโอกาสของคุณ!";
		next;
		mes "[Nils]";
		mes "ฉันแค่อยากจะให้";
		mes "คุณรู้ว่าคุณพิมพ์";
		mes "ข้อความทั้งหมดที่คุณเห็น";
		mes "ในบรรทัดอินพุตเดียวที่";
		mes "คุณได้รับแล้ว ดังนั้นอย่ากดดัน";
		mes "กดปุ่ม enter เพียงคลิก 'ตกลง";
		close;
	case 3:
		mes "[Nils]";
		mes "^0000ff" + $050320_minus1_typing$ + "^000000";
		mes "is the current";
		mes "record holder with";
		mes "a letter total of ^0000ff" + $050320_ein_typing + "^000000";
		mes "characters. Try to beat";
		mes "that record next time~";
		close;
	case 4:
		mes "[Nils]";
		mes "Feel free to take on the";
		mes "^ff0000RO Typing Challenge^000000";
		mes "anytime. I'll be here~";
		close;
	}
}

//============================================================ 
//= Apple Gambling
//============================================================ 
airplane_01,33,68,4	script	Clarice	74,{
	mes "[Clarice]";
	mes "สวัสดี ฉันชื่อคลาริส~";
	mes "คุณต้องการอย่างไร";
	mes "เพื่อเดิมพันแอปเปิล";
	mes "ในเกมลูกเต๋าที่เป็นมิตร?";
	next;
	callfunc "applegamble","Clarice";
	end;
}

function	script	applegamble	{
	.@npc_name$ = getarg(0);
	switch (select("เล่น เกมลูกเต๋า: เรียนรู้ กฎ ของเกมลูกเต๋า : ยกเลิก ")) {
	case 3:
		mes "["+ .@npc_name$ +"]";
		mes "ฉันพร้อมที่จะเล่นเกมแล้ว";
		mes "ลูกเต๋าเมื่อใดก็ตามที่คุณรู้สึก";
		mes "ชอบมัน แค่คุยกับฉันถ้า";
		mes "คุณเคยโดนตีด้วย";
		mes "รู้สึกอยากเล่นพนันขึ้นมาอย่างกะทันหัน ใช่ไหม?";
		close;
	case 2:
		mes "["+ .@npc_name$ +"]";
		mes "กฎกติกาของเกมลูกเต๋า";
		mes "มันง่ายมาก ก่อนอื่น คุณ";
		mes "วางเดิมพันโดยการเดิมพันแอปเปิล.";
		mes "คุณสามารถเดิมพันได้สูงสุด 50";
		mes "แอปเปิลครั้งละลูก เพื่อเก็บของ";
		mes "ถูกกฎหมาย ฉันรับเฉพาะแอปเปิลเท่านั้น";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "But hey, if all that zeny";
		mes "is burning a hole in your";
		mes "pocket, head over to Fruitz";
		mes "and you can buy as many";
		mes "Apples as you want, playah~";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "ตอนนี้เราเริ่มต้นที่ฉัน";
		mes "ทอยลูกเต๋า 6 ด้านสองลูก";
		mes "เมื่อถึงคราวคุณแล้ว คุณจะ";
		mes "ทอยลูกเต๋า 6 ด้าน 2 ลูก หลังจากนั้น";
		mes "ว่าเราทั้งสองจะได้มี";
		mes "ตัวเลือกในการทอยลูกเต๋าครั้งที่สาม";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "ตอนนี้มาถึงสิ่งสำคัญแล้ว";
		mes "สิ่งนั้น ถ้ายอดรวมของคุณสูงกว่า";
		mes "กว่า 12 คุณจะพัง หมายความว่า";
		mes "ที่คุณจะสูญเสีย ไม่เช่นนั้น";
		mes "บุคคลที่มียอดรวมสูงกว่า";
		mes "เป็นผู้ชนะ เข้าใจมั้ย?";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "ตอนนี้คุณจะเป็นคนแรก";
		mes "ที่จะตัดสินใจว่าจะทำหรือไม่";
		mes "คุณจะต้องทอยลูกเต๋าครั้งที่สาม แล้ว";
		mes "ขึ้นอยู่กับผลลัพธ์ของคุณ ฉันจะ";
		mes "ทอยลูกเต๋าครั้งที่สาม... หรืออาจจะไม่";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "เมื่อคุณชนะ คุณจะ";
		mes "รับสองเท่า";
		mes "แอปเปิ้ลตามที่คุณเดิมพัน";
		mes "แต่ถ้าเราบังเอิญเสมอกัน คุณ";
		mes "รับแอปเปิลที่คุณเดิมพันไว้";
		mes "กลับมาหาคุณแล้ว ถูกต้องใช่ไหม?";
		close;
	case 1:
		break;
	}
	mes "["+ .@npc_name$ +"]";
	mes "โอ้ งั้นคุณจะเล่นด้วย";
	mes "ฉันเหรอ? เยี่ยมเลย! กี่คน";
	mes "คุณอยากเดิมพันแอปเปิลไหม?";
	mes "จำไว้ว่าคุณสามารถเดิมพันได้";
	mes "แอปเปิ้ลมากถึง 50 ลูก หากคุณต้องการ";
	mes "หากต้องการยกเลิก กรุณาใส่ '0'.";
	next;
	while(1) {
		input .@amount;
		if (.@amount == 0) {
			mes "["+ .@npc_name$ +"]";
			mes "เปลี่ยนใจแล้วเหรอ?";
			mes "ฉันเข้าใจแล้ว ถ้าอย่างนั้นก็";
			mes "ฉันหวังว่าเราจะได้เล่นกันสักครั้ง";
			close;
		}
		else if (.@amount < 1 || .@amount > 50) {
			mes "["+ .@npc_name$ +"]";
			mes "คุณไม่สามารถเดิมพันมากกว่า";
			mes "แอปเปิ้ล 50 ลูก จำไว้นะเรา";
			mes "ต้องเก็บเดิมพันเหล่านี้ไว้";
			mes "สมเหตุสมผล กรุณากรอก";
			mes "ค่าไม่เกิน 50";
			next;
			continue;
		}
		mes "["+ .@npc_name$ +"]";
		mes "So you'll be";
		mes "เดิมพัน  ^FF0000"+.@amount+"^000000 Apples.";
		mes "นั่นถูกต้องเหรอ?";
		next;
		if (select("ใช่:ไม่ใช่ ") == 2) {
			mes "["+ .@npc_name$ +"]";
			mes "อืม ทำผิดเหรอ?";
			mes "โอเค กรุณาใส่";
			mes "จำนวนแอปเปิลของคุณ";
			mes "ต้องการวางเดิมพันนี้";
			next;
			continue;
		}
		if (countitem(512) <.@amount) {
			mes "ฉันขอโทษ แต่คุณ";
			mes "ดูเหมือนจะไม่มี";
			mes "แอปเปิ้ลเพียงพอสำหรับสิ่งนี้";
			mes "เดิมพันสิ...คุณเล่นการพนันไม่ได้";
			mes "ถ้าเล่นไม่ได้ก็รู้";
			next;
			continue;
		}
		delitem 512,.@amount;
		mes "["+ .@npc_name$ +"]";
		mes "ดี!";
		mes "ตอนนี้เราสามารถเริ่มต้นได้แล้ว";
		mes "เกมนี้! ฉันจะกลิ้งก่อน~";
		break;
	}
	mes "^3355FF*Rolling and rumbling*^000000";
	next;
	set .@giveapple, .@amount*2;
	set .@table1, rand(1,6);
	set .@table2, rand(1,6);
	set .@tablesub, .@table1 + .@table2;
	set .@tabletotal, .@tablesub;
	mes "["+ .@npc_name$ +"]";
	mes "ฉันได้  ^0000FF" + .@table1 + "^000000 และ ^0000FF" + .@table2 + "^000000.";
	mes "นั่นรวมเป็น  ^0000FF" + .@tablesub + "^000000.";
	mes "^FF0000" + strcharinfo(0) + "^000000, ถึงตาคุณแล้ว.";
	next;
	select("โยนลูกเต๋า");
	mes "^3355FF*Rolling and rumbling*^000000";
	set .@player1, rand(1,6);
	set .@player2, rand(1,6);
	set .@playersub, .@player1 + .@player2;
	if (.@playersub > 9 && .@amount > 39) {
		set .@player1, rand(1,6);
		set .@player2, rand(1,6);
		set .@playersub, .@player1 + .@player2;
	}
	set .@playertotal, .@playersub;
	next;
	mes "["+ .@npc_name$ +"]";
	mes "^FF0000" + strcharinfo(0) + "^000000, you have ^FF0000" + .@player1 + "^000000 และ ^FF0000" + .@player2 + "^000000. The total is ^FF0000" + .@playersub + "^000000 .";
	next;
	mes "["+ .@npc_name$ +"]";
	if(.@playersub == .@tablesub) {
		mes "ขณะนี้ยอดรวมของฉันคือ ^0000FF" + .@tablesub + "^000000 และ ^FF0000" + strcharinfo(0) + "^000000, ยอดรวมของคุณคือ ^FF0000" + .@playersub + "^000000. เรากำลังทำให้เกมสมดุล คุณต้องการทอยลูกเต๋าอีกครั้งหรือไม่";
	} else if (.@playersub > .@tablesub) {
		mes "ขณะนี้ยอดรวมของฉันคือ ^0000FF" + .@tablesub + "^000000 และ ^FF0000" + strcharinfo(0) + "^000000, ยอดรวมของคุณคือ  ^FF0000" + .@playersub + "^000000. ^FF0000" + strcharinfo(0) + "^000000, คุณกำลังชนะเกมนี้อยู่ คุณต้องการทอยลูกเต๋าอีกครั้งหรือไม่";
	} else if(.@tablesub > .@playersub) {
		mes "ขณะนี้ยอดรวมของฉันคือ ^0000FF" + .@tablesub + "^000000 และ ^FF0000" + strcharinfo(0) + "^000000, ยอดรวมของคุณคือ  ^FF0000" + .@playersub + "^000000. ฉันชนะเกมนี้ คุณอยากจะทอยลูกเต๋าอีกครั้งไหม";
	}
	next;
	switch (select("โยน ลูกเต๋า : ยกเลิก ")) {
	case 1:
		mes "^3355FF*Rolling and rumbling*^000000";
		set .@player3, rand(1,6);
		set .@playertotal, .@playertotal + .@player3;
		next;
		mes "["+ .@npc_name$ +"]";
		if (.@playertotal > 12) {
			mes "^FF0000" + strcharinfo(0) + "^000000, คุณได้ ^FF0000" + .@player3 + "^000000 และตอนนี้ยอดรวมคือ ^FF0000" + .@playertotal + "^000000. คุณแพ้เกมนี้ ฉันเสียใจ แต่โปรดลองอีกครั้ง";
			close;
		}
		else if (.@playertotal < .@tablesub) {
			mes "^FF0000" + strcharinfo(0) + "^000000, คุณได้ ^FF0000" + .@player3 + "^000000 และตอนนี้ยอดรวมคือ ^FF0000" + .@playertotal + "^000000. แม้ว่าคุณจะทอยลูกเต๋าอีกครั้ง ผลรวมของคุณก็ยังน้อยกว่าของฉัน คุณแพ้เกมแล้ว ฉันขอโทษและโปรดลองใหม่อีกครั้ง";
			close;
		}
		else if (.@playertotal == .@tablesub) {
			if (.@tablesub > 8) {
				mes "^FF0000" + strcharinfo(0) + "^000000, คุณได้ ^FF0000" + .@player3 + "^000000 และตอนนี้ยอดรวมคือ ^FF0000" + .@playertotal + "^000000. ฉันไม่อยากเสี่ยงเลย มาจบเกมนี้ด้วยการเสมอกันดีกว่า มาเล่นกันใหม่คราวหน้า~";
				close2;
				getitem 512,.@amount; //Apple
				end;
			}
			mes "โอเค";
			mes "ให้ฉันทอยลูกเต๋าอีกครั้ง";
		}
		else {
			mes "^FF0000" + strcharinfo(0) + "^000000, คุณได้ ^FF0000" + .@player3 + "^000000 และตอนนี้ยอดรวมคือ ^FF0000" + .@playertotal + "^000000. ทีนี้ก็ถึงตาฉันแล้ว";
		}
		break;
	case 2:
		mes "["+ .@npc_name$ +"]";
		if (.@playersub > .@tablesub) {
			mes "เข้าใจแล้ว คุณไม่อยากเสี่ยงที่จะแพ้เกมหรอก โอเค ให้ฉันทอยลูกเต๋าอีกครั้ง";
		}
		else if (.@playersub == .@tablesub) {
			if (.@tablesub > 8) {
				mes "เข้าใจแล้ว คุณไม่อยากเสี่ยงที่จะแพ้เกมนี้หรอก ฉันก็ไม่อยากเหมือนกัน เรามาจบเกมนี้ด้วยการเสมอกันดีกว่า มาเล่นกันใหม่คราวหน้า~";
				close2;
				getitem 512,.@amount; //Apple
				end;
			}
			mes "โอเค.";
			mes "ให้ฉันทอยลูกเต๋าอีกครั้ง";
		}
		else {
			mes "ลองดูก็ไม่เสียหาย";
			mes "เอาล่ะ ครั้งนี้ฉันชนะ";
			mes "ขอโทษนะ มาลองเล่นกันเถอะ";
			mes "อีกครั้งคราวหน้า";
			close;
		}
		break;
	}
	next;
	mes "^3355FF*Rolling and rumbling*^000000";
	set .@table3, rand(1,6);
	set .@tabletotal, .@tabletotal + .@table3;
	next;
	mes "["+ .@npc_name$ +"]";
	if (.@tabletotal > 12) {
		mes "ฉันได้ ^0000FF" + .@table3 + "^000000 และตอนนี้ยอดรวมเป็น ^0000FF" + .@tabletotal + "^000000. ฉันแพ้เกมนี้เพราะยอดรวมของฉันเกิน 12 แต้ม ขอฉันให้ แอปเปิลแก่ คุณ ขอแสดงความยินดีด้วย เป็นเกมที่ยอดเยี่ยมมาก";
		close2;
		getitem 512,.@giveapple; //Apple
		end;
	}
	else if (.@playertotal > .@tabletotal) {
		mes "ฉันได้ ^0000FF" + .@table3 + "^000000 และตอนนี้ยอดรวมเป็น ^0000FF" + .@tabletotal + "^000000. ด้วยยอดรวม  ^FF0000" + .@playertotal + "^000000 คุณชนะเกมนี้แล้ว , ^FF0000" + strcharinfo(0) + "^000000. ฉันจะให้ แอปเปิลแก่ คุณ มันเป็นเกมที่ยอดเยี่ยมมาก และฉันหวังว่าเราจะได้เล่นกันอีกครั้งในครั้งหน้า";
		close2;
		getitem 512,.@giveapple; //Apple
		end;
	}
	else if (.@playertotal == .@tabletotal) {
		mes "ฉันได้ ^0000FF" + .@table3 + "^000000 และตอนนี้ยอดรวมเป็น ^0000FF" + .@tabletotal + "^000000. ด้วยยอดรวม  ^FF0000" + .@playertotal + "^000000 เกมนี้ออกมาเสมอ, ^FF0000" + strcharinfo(0) + "^000000. ขอคืนแอปเปิลให้หน่อย มันเป็นเกมที่ยอดเยี่ยมมาก และฉันหวังว่าเราจะได้เล่นกันอีกครั้งในครั้งหน้า";
		close2;
		getitem 512,.@amount; //Apple
		end;
	}
	else if (.@playertotal < .@tabletotal) {
		mes "ฉันได้ ^0000FF" + .@table3 + "^000000 และตอนนี้ยอดรวมเป็น ^0000FF" + .@tabletotal + "^000000. ด้วยยอดรวม  ^FF0000" + .@playertotal + "^000000 คุณแพ้เกมนี้, ^FF0000" + strcharinfo(0) + "^000000. ขออภัย โปรดลองอีกครั้ง";
		close;
	}
}
